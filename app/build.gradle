apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 22
  buildToolsVersion "22.0.1"

  def versionMajor = 1
  def versionMinor = 0 // 0-99
  def versionPatch = 0 // 0-99
  def versionBuild = 0 // 0-99

  defaultConfig {
    applicationId "au.id.rlac.unitconverter"
    minSdkVersion 15
    targetSdkVersion 22
    versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  signingConfigs {
    release {
      if (rootProject.file('keystore.properties').exists()) {
        def props = new Properties()
        props.load(new FileInputStream(rootProject.file('keystore.properties')))

        storeFile = file(props['storeFile'])
        storePassword = props['storePassword']
        keyAlias = props['keyAlias']
        keyPassword = props['keyPassword']
      }
    }
  }

  buildTypes {
    debug {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "com.android.support:support-v13:22.2.0"
  compile "com.android.support:appcompat-v7:22.2.0"
  compile "com.android.support:design:22.2.0"
  compile "info.android15.nucleus:nucleus:1.3.0"
  compile "me.grantland:autofittextview:0.2.1"
}
